# -*- mode: sh -*-
# vim: set ft=sh:

function zpwrConvertToRelPath(){
    while read; do
        zpwrRelPath $REPLY
    done
}


function zpwrFzfFileSearch(){

    emulate -LR zsh
    local filter=cat top noslash

    if [[ -n "$1" ]]; then
        filter="$1"
    fi

    if zpwrIsGitDir; then
        top=$(git rev-parse --show-toplevel)
        if [[ $PWD == $top ]]; then
            if git check-ignore -q . &>/dev/null; then
                ${=ZPWR_FD} --no-ignore . ./ 2>/dev/null
            else
                ${=ZPWR_FD} . ./ 2>/dev/null
            fi
        else
            if git check-ignore -q . &>/dev/null; then
                ${=ZPWR_FD} --no-ignore . $top 2>/dev/null
            else
                ${=ZPWR_FD} . $top 2>/dev/null
            fi | zpwrConvertToRelPath
        fi
    else
        ${=ZPWR_FD} . ./ 2>/dev/null | cut -c3-
    fi | ${=filter} |
        eval "$ZPWR_FZF -m --border $FZF_CTRL_T_OPTS" |
        perl -ne 'chomp $_; print "\"$_\" "'
}

zpwrFzfFileSearch "$@"
