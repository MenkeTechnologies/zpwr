# -*- mode: sh -*-
# vim: set ft=sh:

function zpwrCd(){

    builtin emulate -L zsh

    local base dir noTrailSlash

    noTrailSlash="${1%/}"

    if [[ -z "$noTrailSlash" ]]; then
        cd -
        return 0
    elif [[ -f "$noTrailSlash" ]]; then
        cd "$(dirname "$noTrailSlash")"
    elif [[ -d "$noTrailSlash" ]];then
        cd "$noTrailSlash"
    elif [[ -d "${nameddirs[$noTrailSlash]}" ]];then
        cd "${nameddirs[$noTrailSlash]}"
    elif [[ -d "${userdirs[$noTrailSlash]}" ]];then
        cd "${userdirs[$noTrailSlash]}"
    else
        #cd -1
        echo "$noTrailSlash" | command grep -E '\-[0-9]+' && cd "$noTrailSlash" && return 0

        #cd to first partial name match
        if zpwrIsZsh;then
            eval 'for dir in (#i)'"$noTrailSlash"'*(N/);do
                cd "$dir"
                return
            done'
        fi

        base="$(dirname "$noTrailSlash")"

        if [[ $base == "." ]]; then
            zpwrLogConsoleErr "'$noTrailSlash': Not a valid file or directory."
            return 1
        fi

        #find next dir to root
        while [[ "$base" != / ]]; do
            test -d "$base" && cd "$base"&& return 0
            base="$(dirname "$base")"
        done
        zpwrLogConsoleErr "'$noTrailSlash': Not a valid file or directory."
        return 1
    fi

  }

zpwrCd "$@"
