# -*- mode: sh -*-
# vim: set ft=sh:
function zpwrExecGlob(){

    if [[ -z "$2" ]]; then
        zpwrLoggErr "usage: zpwrExecGlob <glob> <command with auto \$f replacement and 1 level of eval>"
        return 1
    fi
    emulate -L zsh
    setopt noshwordsplit extendedglob globdots nullglob globsubst

    local glob command files failed f ky

    glob="$1"
    command="$2"
    shift
    shift

    files=( ${glob} )
    failed=()

    if (( $#files )); then
        zpwrPrettyPrint "Processing ${#files} files"
    else
        eval :
        return 0
    fi

    for f in "${files[@]}"; do
        key="$$-${f:t}-$(du "$f" | awk '{print $1}')"
        if ! (( ${+ZPWR_PROCESSED[$key]} )); then
            zpwrLoggInfo "${(e)command}"
            if eval "${(e)command}"; then
                ZPWR_PROCESSED[$key]="${f:A}"
            else
                failed+=("$f")
            fi

        fi
    done

    for f in "${failed[@]}"; do
        zpwrLoggErr "FAILED: ${(e)command}"
    done

}

zpwrExecGlob "$@"
