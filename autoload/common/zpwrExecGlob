# -*- mode: sh -*-
# vim: set ft=sh:
function zpwrExecGlob(){

    if [[ -z "$2" ]]; then
        zpwrLogConsoleErr "usage: zpwrExecGlob <glob> <command with auto \$f replacement and 1 level of eval>"
        return 1
    fi
    builtin emulate -L zsh
    setopt noshwordsplit extendedglob globdots nullglob globsubst

    if ! (( $+ZPWR_PROCESSED )); then
        typeset -Ag ZPWR_PROCESSED
    fi

    local glob command files failed f ky

    glob="$1"
    command="$2"
    shift
    shift

    files=( ${glob} )
    failed=()

    if (( $#files )); then
        zpwrPrettyPrint "Processing ${#files} files"
    else
        eval :
        return 0
    fi

    for f in "${files[@]}"; do
        key="$$-${f:t}-$(du "$f" | awk '{print $1}')"
        if ! (( ${+ZPWR_PROCESSED[$key]} )); then
            zpwrLogConsoleInfo "${(e)command}"
            if eval "${(e)command}"; then
                ZPWR_PROCESSED[$key]="${f:A}"
            else
                failed+=("$f")
            fi
        else
            ZPWR_PROCESSED[$key]="${f:A}"
        fi
    done

    for f in "${failed[@]}"; do
        zpwrLogConsoleErr "FAILED: ${(e)command}"
    done

}

zpwrExecGlob "$@"
