# -*- mode: sh -*-
# vim: set ft=sh:
function recompile(){

    emulate -L zsh
    setopt nullglob

    local dir files sudoFiles file

    files=(
        "$HOME/.zshrc"*
        "$HOME/.zshenv"*
        "$HOME/.zlogout"*
        "$HOME/.zlogin"*
        "$HOME/grc.zsh"
        "$ZSH/oh-my-zsh.sh"
        "$ZSH_COMPDUMP"
        "$ZPWR_ALIAS_FILE"
        "$ZPWR_PROMPT_FILE"
        "$ZPWR_LOCAL/.tokens.sh"
        "$ZPWR_LOCAL/.tokens-post.sh"
        "$ZPWR_ENV_FILE"
        "$ZPWR_RE_ENV_FILE"
        "$ZPWR_SCRIPTS/zpwr.zsh"
        "$ZPWR_LIB"
        "$ZPWR_LIB_INIT"
        "$ZPWR_SCRIPTS/crossOSCommands.sh"
        "$ZSH/lib/"*.zsh
        "$ZSH/plugins/"*/*.plugin.zsh
        "$ZSH/themes/"*.zsh-theme
        "$ZSH_CUSTOM/plugins/"*/*.plugin.zsh
        "$ZSH_CUSTOM/plugins/"*/*.plugin.zsh
        "$ZSH_CUSTOM/themes/"*/*.zsh-theme
        "$ZSH_CUSTOM/lib/"*.zsh
        "$ZSH_CUSTOM/snippets/OMZ::plugins/"*/*.zsh
        "$ZSH_CUSTOM/snippets/OMZ::lib/"*/*.zsh
        "$ZSH_CUSTOM/snippets/OMZ::plugins/"*/*.plugin.zsh
    )

    sudoFiles=(
        /etc/profile*
        /etc/zprofile*
        /etc/zshrc*
        /etc/zshenv*
        /etc/zlogin*
        /etc/zlogout*
        /etc/zsh/z*
        /etc/profile.env*
    )

    prettyPrint "recompiling all configs to .zwc for speed"

    for file in ${files[@]}; do
        if [[ -f "$file" ]]; then
            zrecompile -p "$file"
        fi
    done
    for file in ${sudoFiles[@]}; do
        if [[ -f "$file" ]]; then
            sudo zsh -c "autoload zrecompile; zrecompile -p $file"
        fi
    done

	for dir in $fpath; do
		if test -d $dir;then
            files=( $dir/*(N) )
            if (($#files)); then
                zrecompile -p $dir.zwc $files 2> /dev/null ||
                sudo zsh -c "autoload zrecompile; zrecompile -p $dir.zwc $files"
            fi
        fi
	done
}

recompile "$@"
