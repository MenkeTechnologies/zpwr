# -*- mode: sh -*-
# vim: set ft=sh:
# redefine global zsh completion function called at first parameter
# adding global aliases and files

function _command_names(){

    # The option `-e' if given as the first argument says that we should
    # complete only external commands and executable files. This and a
    # `-' as the first argument is then removed from the arguments.

    local args defs ffilt aliasesAry galiasesAry k v

    zstyle -t ":completion:${curcontext}:commands" rehash && rehash

    zstyle -t ":completion:${curcontext}:functions" prefix-needed &&
    [[ $PREFIX != [_.]* ]] && ffilt='[(I)[^_.]*]'

    defs=(
        'commands:external command:_path_commands'
    )

    [[ -n "$path[(r).]" || $PREFIX = */* ]] &&
        defs+=( 'executables:executable file:_files -g \*\(-\*\)' )

    if [[ "$1" = -e ]]; then
        shift
    else
        [[ "$1" = - ]] && shift

        defs+=(
            'global-aliases:global alias:__zpwr_galiases'
            'aliases:alias:__zpwr_aliases'
            "functions:shell function:compadd -k 'functions$ffilt'"
            'builtins:builtin command:compadd -Qk builtins'
            'suffix-aliases:suffix alias:_suffix_alias_files'
            'reserved-words:reserved word:compadd -Qk reswords'
            'jobs:: _jobs -t'
            'parameters:: _parameters'
            'files:files:_files'
        )
    fi

    args=( "$@" )

    local -a cmdpath
    if zstyle -a ":completion:${curcontext}" command-path cmdpath && (( $#cmdpath )); then
        local -a +h path
        local -A +h commands
        path=( $cmdpath )
    fi
    _alternative -O args "$defs[@]"
}

_command_names "$@"
