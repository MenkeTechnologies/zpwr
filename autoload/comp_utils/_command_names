# -*- mode: sh -*-
# vim: set ft=sh:
# redefine global zsh completion function called at first parameter
# adding global aliases and files
__aliases(){

    local k v

    if ( ! (( $+__aliases_ary )) || _cache_invalid alias_cache ) && ! _retrieve_cache alias_cache; then
        for k v in ${(kv)aliases}; do
            __aliases_ary+=($k:"${(q)v}")
        done

        _store_cache alias_cache __aliases_ary
    fi

    _describe -t aliases alias __aliases_ary
}
__galiases(){

    local galiasesAry
    local k v

    if ( ! (( $+__galiases_ary )) || _cache_invalid galias_cache ) && ! _retrieve_cache galias_cache; then
        for k v in ${(kv)galiases}; do
            __galiases_ary+=($k:"${(q)v}")
        done

        _store_cache galias_cache __galiases_ary
    fi

    _describe -t global-aliases 'global alias' __galiases_ary
}
function _command_names(){

    # The option `-e' if given as the first argument says that we should
    # complete only external commands and executable files. This and a
    # `-' as the first argument is then removed from the arguments.

    local args defs ffilt aliasesAry galiasesAry k v

    zstyle -t ":completion:${curcontext}:commands" rehash && rehash

    zstyle -t ":completion:${curcontext}:functions" prefix-needed &&
    [[ $PREFIX != [_.]* ]] &&
    ffilt='[(I)[^_.]*]'

    defs=(
        'commands:external command:_path_commands'
    )

    [[ -n "$path[(r).]" || $PREFIX = */* ]] &&
        defs+=( 'executables:executable file:_files -g \*\(-\*\)' )

    if [[ "$1" = -e ]]; then
    shift
    else
    [[ "$1" = - ]] && shift

    defs=( 
        "$defs[@]"
        'global-aliases:global alias:__galiases'
        'aliases:alias:__aliases'
        "functions:shell function:compadd -k 'functions$ffilt'"
        'builtins:builtin command:compadd -Qk builtins'
        'suffix-aliases:suffix alias:_suffix_alias_files'
        'reserved-words:reserved word:compadd -Qk reswords'
        'jobs:: _jobs -t'
        'parameters:: _parameters -g "^*(readonly|association)*" -qS= -r "\n\t\- =[+"'
        'parameters:: _parameters -g "*association*~*readonly*" -qS\[ -r "\n\t\- =[+"'
        'files:files:_files'
    )
    fi

    args=( "$@" )

    local -a cmdpath
    if zstyle -a ":completion:${curcontext}" command-path cmdpath &&
        [[ $#cmdpath -gt 0 ]]
    then
        local -a +h path
        local -A +h commands
        path=( $cmdpath )
    fi
    _alternative -O args "$defs[@]"
}

_command_names "$@"
