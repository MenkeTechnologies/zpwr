# -*- mode: sh -*-
# vim: set ft=sh:
function _parameters() {

    # function_body
    local expl pattern tmp pfilt i maxLen ary
    local -a fakes faked
    if compset -P '*:'; then
        _history_modifiers p
        return
    fi

    pattern=(-g \*)
    zparseopts -D -K -E g:=pattern

    fakes=()
    faked=()
    if zstyle -a ":completion:${curcontext}:" fake-parameters tmp; then
    for i in "$tmp[@]"; do
        if [[ "$i" = *:* ]]; then
        faked=( "$faked[@]" "$i" )
        else
        fakes=( "$fakes[@]" "$i" )
        fi
    done
    fi

    zstyle -t ":completion:${curcontext}:parameters" prefix-needed &&
    [[ $PREFIX != [_.]* ]] && \
    pfilt='[^_.]'

    typeset -a ary
    maxLen=50
    for i in "${(@M)${(@k)parameters[(R)${pattern[2]}~*local*]}:#${~pfilt}*}"; do
        ary+=($i:"${${(P)i}:0:100}")
    done

    for i in "$fakes[@]"; do
        ary+=($i:"${(P)i:0:100}")
    done

    for i in "${(@)${(@M)faked:#${~pattern[2]}}%%:*}"; do
        ary+=($i:"${(P)i:0:100}")
    done

    _describe -t parameters parameter ary

}

_parameters "$@"
