#compdef f

ZPWR_HAS_Z=true
ZPWR_HAS_FASD=true

if zpwrExists zshz; then
    zcmd=zshz
elif zpwrExists _z; then
    zcmd=_z
else
    ZPWR_HAS_Z=false
fi

if ! zpwrExists fasd;then
    ZPWR_HAS_FASD=false
fi

__z_dirs_comp(){

    if ! (( $+__z_dirs )) && ! _retrieve_cache z_dirs_cache; then
        __z_dirs=( $($zcmd -l |& perl -e '@l=reverse<>;do{print "$2:$1 " if m{^\s*(\S+)\s+(\S+)\s*$}}for@l') )
        _store_cache z_dirs_cache __z_dirs
    fi

    _describe -t zdir "z ranked directories" __z_dirs
}
__fasd_dirs_comp(){

    if ! (( $+__fasd_dirs )) && ! _retrieve_cache fasd_dirs_cache; then
        __fasd_dirs=( $(fasd -d |& perl -e '@l=reverse<>;do{print "$2:$1 " if m{^\s*(\S+)\s+(\S+)\s*$} }for@l') )
        _store_cache fasd_dirs_cache __fasd_dirs
    fi

    _describe -t fasd "fasd ranked directories" __fasd_dirs
}

function _f(){

    _alternative \
        'files:directory:_path_files -g "*(-D/)"' && return 0

    local cmd ret

    if (( $#words > 1 )); then
        if [[ "$ZPWR_HAS_FASD" == true ]]; then

            if [[ "$ZPWR_HAS_Z" == true ]]; then
                    _alternative \
                    'fasd:fasd ranked directories:__fasd_dirs_comp' \
                    'zdir:z ranked directories:__z_dirs_comp' \
                    'directory-stack:directory stack:_directory_stack'
                else
                    _alternative \
                    'fasd:fasd ranked directories:__fasd_dirs_comp' \
                    'directory-stack:directory stack:_directory_stack'
            fi
        else
            if [[ "$ZPWR_HAS_Z" == true ]]; then
                    _alternative \
                    'zdir:z ranked directories:__z_dirs_comp' \
                    'directory-stack:directory stack:_directory_stack'
                else
                    _alternative \
                    'directory-stack:directory stack:_directory_stack'
            fi
        fi
        return 1
    fi

}
_f "$@"
